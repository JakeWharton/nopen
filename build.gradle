buildscript {
  ext.versions = [
      'errorProne': '2.3.4',
  ]

  ext.deps = [
      'errorProne': [
          'core': "com.google.errorprone:error_prone_core:${versions.errorProne}",
          'checkApi': "com.google.errorprone:error_prone_check_api:${versions.errorProne}",
          'testHelpers': "com.google.errorprone:error_prone_test_helpers:${versions.errorProne}",
      ],
      'autoService': "com.google.auto.service:auto-service:1.0-rc6",
      'junit': 'junit:junit:4.13',
      'truth': 'com.google.truth:truth:0.46',
      'compileTesting': 'com.google.testing.compile:compile-testing:0.18',
  ]

  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
  }

  dependencies {
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:1.1.1'
    classpath 'com.android.tools.build:gradle:3.6.2'
  }
}

subprojects {
  apply plugin: 'net.ltgt.errorprone'

  repositories {
    mavenCentral()
    google()
    jcenter {
      // Required for a dependency of Android lint.
      content {
        includeGroup 'org.jetbrains.trove4j'
      }
    }
  }

  dependencies {
    errorprone "com.google.errorprone:error_prone_core:${versions.errorProne}"
    errorproneJavac "com.google.errorprone:javac:9+181-r4173-1"
  }
}
